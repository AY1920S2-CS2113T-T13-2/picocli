buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.beryx:badass-jar:1.1.3"
    }
}

plugins {
    id 'java'
}

apply plugin: "org.beryx.jar"

group 'info.picocli'
description 'Picocli JPMS module: compiles the module-info.class to make the picocli jar a modular jar'
version "$projectVersion"
ext.moduleName = 'info.picocli'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


compileJava {
    inputs.property("moduleName", moduleName)   
    doFirst {
        if (org.gradle.api.JavaVersion.current().isJava9Compatible()) {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
        }
        classpath = files()
    }
}

jar {
    // copy module-info.class to META-INF/versions/9
    multiRelease = true
}
