buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.beryx:badass-jar:1.1.3"
    }
}

plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
    id 'com.jfrog.bintray'
    id 'java-library'
    //id 'org.beryx.jar' version '1.1.3'
}

apply plugin: "org.beryx.jar"

group 'info.picocli'
description 'Picocli JPMS module: a modular jar for applications that want to use picocli while taking full advantage of the Java Platform Module System (JPMS) introduced in Java 9'
version "$projectVersion"
ext.moduleName = 'info.picocli'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task copyRootProjectCode(type: Copy) {    
    from("${rootProject.projectDir}/src/main/java"){
        exclude '**/groovy'
    }
    into "${projectDir}/src/main/java"
}
task cleanupRootProjectCode(type: Delete) {
    delete "${projectDir}/src/main/java/picocli"
    delete fileTree("${projectDir}/src/main/java") {
        exclude '**/module-info.java'
    }
}

compileJava {
    inputs.property("moduleName", moduleName)   
    doFirst {
        if (org.gradle.api.JavaVersion.current().isJava9Compatible()) {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
        }
        classpath = files()
    }
}
compileJava.dependsOn(copyRootProjectCode)
assemble.finalizedBy(cleanupRootProjectCode)
clean.finalizedBy(cleanupRootProjectCode)


test {
    // Excluding unit tests, should have been generated already with the root project.
    exclude 'picocli'
}

jar {
    manifest {
        attributes 'Specification-Title'   : 'picocli-jpms-module',
                   'Specification-Vendor'  : 'Remko Popma',
                   'Specification-Version' : version,
                   'Implementation-Title'  : 'picocli-jpms-module',
                   'Implementation-Vendor' : 'Remko Popma',
                   'Implementation-Version': version,
                   'Main-Class'            : 'picocli.AutoComplete'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }    
}


ext {
    bintrayPackage    = 'picocli-jpms-module'
    bintrayWebsiteUrl = 'https://github.com/remkop/picocli/tree/master/picocli-jpms-module'
    bintrayLabels     = ['jpms', 'module', 'java9', 'picocli']
}
bintray {
    user = bintrayUsername
    key  = bintrayApiKey
    publications = ['MyPublication']
    dryRun   = bintrayDryRun //[Default: false] Whether to run this as dry-run, without deploying
    publish  = bintrayPublish //[Default: false] Whether version should be auto published after an upload    
    override = bintrayOverride //[Default: false] Whether to override version artifacts already published    
    //Package configuration. The plugin will use the repo and name properties to check if the package already exists. In that case, there's no need to configure the other package properties (like userOrg, desc, etc).
    pkg {
        repo     = 'picocli'
        name     = bintrayPackage
        userOrg  = 'remkop'
        licenses = ['Apache-2.0']
        desc = description
        websiteUrl = bintrayWebsiteUrl
        issueTrackerUrl = 'https://github.com/remkop/picocli/issues'
        vcsUrl   = 'https://github.com/remkop/picocli.git'
        labels = bintrayLabels
        publicDownloadNumbers = false
        version {
            name     = "$projectVersion"
            desc     = description
            released = new Date()
            vcsTag   = "v$projectVersion"
            mavenCentralSync {
                sync = mavenOssSync //[Default: true] Determines whether to sync the version to Maven Central.
                user = mavenOssUser //OSS user token: mandatory
                password = mavenOssPassword //OSS user password: mandatory
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact testJar
            artifact testSourcesJar
            artifact javadocJar
            groupId 'info.picocli'
            artifactId bintrayPackage
            version "$projectVersion"
            pom.withXml {
                def root = asNode()
                root.appendNode('packaging',     'jar')
                root.appendNode('name',          bintrayPackage)
                root.appendNode('description',   description)
                root.appendNode('url',           'http://picocli.info')
                root.appendNode('inceptionYear', '2019')
                root.children().last() + pomConfig
            }
        }
    }
}
